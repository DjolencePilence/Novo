package rs.ac.bg.etf.pp1;
import java_cup.runtime.*;
//import java.io.*;
import org.apache.log4j.*;
import java.util.Stack;
import rs.etf.pp1.symboltable.Tab;
import rs.etf.pp1.symboltable.concepts.*;
import rs.etf.pp1.mj.runtime.Code;

parser code {:
	
	boolean errorDetected = false;
	
	int numGlobVar = 0;
	int numGlobConst = 0;
	int numGlobArrays = 0;
	int numLocalVar = 0;
	int printCallCount = 0;
	int numFuncCallsInMain = 0;
	int numFormParam = 0;
	int numGlobFunc = 0;
	int numStaticFunc = 0;
	
	boolean insideOfClass = false;
	boolean isStaticFunc = false;
	
	Struct boolType = null;
	
	
	Stack<String> stack = new Stack<String>();
	
	Logger log = Logger.getLogger(getClass());
   
   	//Metoda za ispis nadjenih simbola
   	private String stringKind(int kind){
	   	switch(kind){
	   			case 0: return "noType";
	   			case 1: return "int" ;
	   			case 2: return "char";
	   			case 3: return "Arr of ";
	   			case 4: return "Class";
	   			case 5: return "bool";
	   			default: return " ";
	   		}
   	}
   
   	public void printSymbol(int numLine, String varIdent, Obj node){
   		int kind = node.getType().getKind();
   		String tip = stringKind(kind);
   		if(kind ==3) tip += stringKind(node.getType().getElemType().getKind());
   		if(node.getLevel()==0){
			if(node.getKind()== Obj.Con)
				report_info("Pretraga na " + numLine + "("+varIdent+"), nadjeno Con " + varIdent +" " + tip+ ", "+node.getAdr() +","+node.getLevel(), null);
			else
				report_info("Pretraga na " + numLine + "("+varIdent+"), nadjeno Var " + varIdent +" " + tip+ ", "+node.getAdr() +","+node.getLevel(), null);
			}
		else
			report_info("Pretraga na " + numLine + "("+varIdent+"), nadjeno Var " + varIdent +" " + tip+ ", "+node.getAdr() +","+node.getLevel(), null);
   			
   	}
   	public Obj checkExistence(int numLine, String varIdent){
   			Obj node = Tab.find(varIdent);
	   		if(node == Tab.noObj) 
	   			report_error("Greska na " + numLine +" ("+ varIdent + ") nije nadjeno", null);
	   		else{
	   			printSymbol(numLine, varIdent, node);
	   		}
	   		if(node.getKind() == Struct.Array) node = new Obj(Obj.Elem, "", node.getType().getElemType());
	   		return node;
   	}
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

init with {:
	stack.push("global");
	Tab.init();
	boolType = new Struct(Struct.Bool);
	Tab.currentScope.addToLocals(new Obj(Obj.Type,"bool",boolType));
:}

action code {:
	Struct typeName = null;
	String varIdent = null;
	String constIdent = null;
	int numLine = 0;
	
	Struct returnValue = null;
	Obj currentMethod = null;
	boolean returnFound = false;
	Scope mainScope = null;
	boolean hasArguments= true;
	Obj array = null;
:}


terminal Integer NUMBER, EQUAL;
terminal String IDENT;
terminal Character CHAR;
terminal Integer BOOL;
terminal PROG, PRINT, RETURN, EXTENDS, NEW, ELSE, IF, FOR, STATIC, CONST, BREAK, READ, CLASS, CONTINUE;
terminal PLUS, COMMA, SEMI, LPAREN, RPAREN, LBRACE, RBRACE, LZAG, RZAG, MINUS, MULTIPLY, DIVIDE, PLUSEQUAL, MINUSEQUAL;
terminal MULTIPLYEQUAL, DIVIDEEQUAL, MOD, EE, NOTEQUAL, LESS, LESSEQUAL, GREATER, GREATEREQUAL, ME, AND, OR, PLUSPLUS, MINUSMINUS;
terminal POINT, VOID;

non terminal Program, DeclarationList, Declaration, ConstDecl, VarDecl, MethodDecl, MethodDeclList, ConstType, ConstDeclArray;
non terminal VarDeclZag, VarDeclArray, ClassDecl, ClassExtends, VarDeclListOptional, MethodDeclListOptional, StaticOptional;
non terminal VarDeclList, FormalParamList, FormalParam, StatementList, Statement, DesignatorStatement;
non terminal Condition, DesignatorStatementOptional, ConditionOptional, ActParsList, ActPars;
non terminal CondTerm, CondFact, CondFactList, CondTermList,  Relop;
non terminal RelExprOptional, DeclarationListOptional ;
non terminal ClassVarDeclListOptional, ClassVarDeclList, ClassVarDeclArray, ClassVarDecl;
non terminal VarArgs, Parameters;
non terminal GlobVarDecl, GlobVarDeclZag, GlobVarDeclArray;
non terminal Obj ProgName;
non terminal Struct Type;
non terminal GlobVarTypeAndName;
non terminal VariableName;
non terminal ConstName, TypeOfConstant;
non terminal Struct RetValue;
non terminal LocalVarType;
non terminal FormVarDeclZag;
non terminal Obj Designator, DesignatorList, DesignatorListElement;
non terminal Struct  Expr, Factor, Term;
non terminal Struct FactorExpr, FactorExprLeft;
non terminal Struct SubExpr, SubExprLeft;
non terminal Integer AddopLeft, AddopRight, MulopLeft, MulopRight, Assignop;

precedence left ELSE;

Program ::= PROG ProgName:p DeclarationListOptional LBRACE MethodDeclList RBRACE
{:
	Code.dataSize = Tab.currentScope().getnVars();
	Tab.chainLocalSymbols(p);
	Tab.closeScope();
:};

ProgName ::= IDENT:pName
{:
	RESULT = Tab.insert(Obj.Prog, pName, Tab.noType);
	Tab.openScope();
	mainScope = Tab.currentScope;
:};

DeclarationListOptional ::= DeclarationList
							|
							/* */
							;

DeclarationList ::= Declaration DeclarationList
					|
					Declaration;
					
Declaration ::=	ConstDecl
				|
				GlobVarDecl
				|
				ClassDecl
				;

// Deklaracija KONSTANTI
		
ConstType ::= BOOL:c
			 {:
			 	if(typeName!=parser.boolType) parser.report_error("Linija: Konstanta " +constIdent +"mora biti tipa bool ", null);
			 	else{
			 		Obj node = Tab.find(constIdent);
					if(node != Tab.noObj) parser.report_error("Vec je deklarisano ime " + constIdent,null);
					else{
						parser.report_info("Definisana konstanta " + constIdent+ " na liniji " + numLine, null);
						node = Tab.insert(Obj.Con, constIdent, typeName);
						node.setAdr(c.intValue());
					}
			 	}
			 :}
			  |
			  NUMBER:c
			  {:
			 	if(typeName!=Tab.intType) parser.report_error("Linija:"+cleft + " Konstanta mora biti tipa int ", null);
			 	else{
			 		Obj node = Tab.find(constIdent);
					if(node != Tab.noObj) parser.report_error("Vec je deklarisano ime " + constIdent,null);
					else{
						parser.report_info("Definisana konstanta " + constIdent+ " na liniji " + numLine, null);
						node = Tab.insert(Obj.Con, constIdent, typeName);
						node.setAdr(c.intValue());
					}
			 	}
			 :}
			  |
			  CHAR:c
			  {:
			 	if(typeName!=Tab.charType) parser.report_error("Konstanta "+ constIdent + " mora biti tipa char ", null);
			 	else{
			 		Obj node = Tab.find(constIdent);
					if(node != Tab.noObj) parser.report_error("Vec je deklarisano ime " + constIdent,null);
					else{
						parser.report_info("Definisana konstanta " + constIdent+ " na liniji " + numLine, null);
						node = Tab.insert(Obj.Con, constIdent, typeName);
						node.setAdr(c);
					}
			 	}
			 :}
			  ;
		
ConstDeclArray ::= COMMA ConstName EQUAL ConstType ConstDeclArray
				 |
				 COMMA ConstName EQUAL ConstType SEMI; 

TypeOfConstant ::= Type:constType
{:
	typeName = constType;
:};

ConstName ::= IDENT:constName
{:	
	parser.numGlobConst++;
	constIdent = constName;
	numLine = constNameleft;
	
:};
	
ConstDecl ::= CONST	TypeOfConstant ConstName EQUAL ConstType SEMI
			  |
			  CONST	TypeOfConstant ConstName EQUAL ConstType ConstDeclArray;  

//Kraj deklaracije KONSTANTI

// GLOBAL VARIABLES AND ARRAYS START ------------------------------------------

GlobVarDeclZag ::= LZAG RZAG
					{:  parser.numGlobArrays++; parser.numGlobVar++;
						Obj node = Tab.currentScope.findSymbol(varIdent);
						if(node!=null && node != Tab.noObj ) parser.report_error("Vec je deklarisan globalan niz sa imenom " + varIdent,null);
				  		else{
							parser.report_info("Deklarisan globalni niz " + varIdent+ " na liniji " + numLine, null);
							Tab.insert(Obj.Var, varIdent, new Struct(Struct.Array,typeName));
						}
					
					:}
					|
					/*epsilon*/ 
					{:  parser.numGlobVar++;
						Obj node = Tab.currentScope.findSymbol(varIdent);
						if(node!=null && node != Tab.noObj ) parser.report_error("Vec je deklarisana globalna promenljiva sa imenom " + varIdent,null);
				  		else{
							parser.report_info("Deklarisana globalna promenljiva " + varIdent+ " na liniji " + numLine, null);
							Tab.insert(Obj.Var, varIdent, typeName);
						}
					:}
					;
					
VariableName ::= IDENT:varName
{:
	numLine = varNameleft;
	varIdent = varName;
:};
					
GlobVarDeclArray ::= COMMA VariableName GlobVarDeclZag GlobVarDeclArray
					 |
					 COMMA error GlobVarDeclArray
					 |
					 COMMA VariableName  GlobVarDeclZag SEMI
					 |
					 COMMA error SEMI;

GlobVarTypeAndName ::= Type:varType IDENT:varName
{:
					varIdent = varName;
					typeName = varType;
					numLine = varNameleft;
:};

				 		  
GlobVarDecl ::= GlobVarTypeAndName GlobVarDeclZag SEMI
				|
				error SEMI
				|
				GlobVarTypeAndName GlobVarDeclZag GlobVarDeclArray
				;


// GLOBAL VARIABLES AND ARRAYS END ------------------------------------------

//LOCAL VARIABLES -----------------------------------------------------------

LocalVarType ::= Type:varType 
{:
	typeName = varType;
:};

VarDeclZag ::= LZAG RZAG
				{: if("main".equals(parser.stack.peek())) parser.numLocalVar++; 
				  	//check if already exists
				  	Obj node = Tab.currentScope.findSymbol(varIdent);
				  	if(node!=null && node != Tab.noObj) parser.report_error("Vec je deklarisan niz sa imenom " + varIdent,null);
				  	else{
						parser.report_info("Deklarisan lokalni niz " + varIdent+ " na liniji " + numLine, null);
						Tab.insert(Obj.Var, varIdent, new Struct(Struct.Array,typeName));
					}
				:}
				|
				/*epsilon*/
				{:  if("main".equals(parser.stack.peek())) parser.numLocalVar++;
					Obj node = Tab.currentScope.findSymbol(varIdent); 
					if(node!=null && node != Tab.noObj) parser.report_error("Vec je deklarisana promenljiva sa imenom " + varIdent,null);
				  	else{
						parser.report_info("Deklarisana lokalna promenljiva " + varIdent+ " na liniji " + numLine, null);
						Tab.insert(Obj.Var, varIdent, typeName);
					}
				:}
				;
				
VarDeclArray ::= COMMA VariableName VarDeclZag VarDeclArray
				 |
				 COMMA VariableName VarDeclZag SEMI;
				 		  
VarDecl ::= LocalVarType VariableName VarDeclZag SEMI
			|
			error SEMI
			|
			error
			|
			LocalVarType VariableName VarDeclZag VarDeclArray;

VarDeclList ::= VarDeclList VarDecl
				|
				VarDecl;

VarDeclListOptional ::= VarDeclList
					|
					/*epsilon*/
					;
// LOCAL VARIABLES END ---------------------------------------------------

// CLASSES START ---------------------------------------------------------
			
ClassExtends ::= EXTENDS Type
				 |
				 /* epsilon */
				 ;
// MODIFIKACIJA 1    JUL
ClassVarDeclListOptional ::= ClassVarDeclList
							|
							/*epsilon */
							;
ClassVarDeclList ::= ClassVarDeclList ClassVarDecl 
					 |
					 ClassVarDecl
					 ;
ClassVarDeclArray ::= COMMA VariableName VarDeclZag ClassVarDeclArray
					  |
					  COMMA VariableName VarDeclZag SEMI;
					  
ClassVarDecl ::= StaticOptional LocalVarType VariableName VarDeclZag SEMI
				|
				StaticOptional LocalVarType VariableName VarDeclZag ClassVarDeclArray;
							
ClassDecl ::= CLASS IDENT:className {:	  parser.insideOfClass=true;
										  Tab.insert(Obj.Type,className,Tab.nullType );
										  Tab.openScope();
								    :} ClassExtends LBRACE ClassVarDeclListOptional MethodDeclListOptional RBRACE
									{:	   parser.insideOfClass = false;
										   Obj node = Tab.find(className);
										   Tab.chainLocalSymbols(node);
										   Tab.closeScope();
									:};

// METHOD STARTS -----------------------------------------------------

MethodDeclListOptional ::= LBRACE MethodDeclList RBRACE
						|
					   /*epsilon */
					   ;
					   							


MethodDeclList ::= MethodDeclList MethodDecl
					|
				   MethodDecl
				   ;
				   
StaticOptional ::= STATIC
				   {: parser.isStaticFunc=true; :}
				   |
				   /*epsilon */
				   {: parser.isStaticFunc=false; :}
				   ;
RetValue ::= VOID {: RESULT = Tab.noType; :}
			|
			Type {: RESULT = returnValue; :};

//FormPars ::= FormalParamList
//			|
//			/*epsilon*/
//			;
			
FormalParamList ::= FormalParamList COMMA FormalParam
					{:parser.numFormParam++; :}
					|
					FormalParam
					{:parser.numFormParam++; :}
					;

FormVarDeclZag ::= LZAG RZAG
				 |
				 /*	epsilon */
				 ;

FormalParam ::= Type IDENT FormVarDeclZag
				|
				error;
 							
VarArgs ::= Type POINT POINT POINT IDENT;

Parameters ::= FormalParamList COMMA VarArgs	{: hasArguments=true; :}
			   |
			   VarArgs	{: hasArguments=true; :}
			   |
			   FormalParamList {: hasArguments=true; :}
			   |
			   /*epsilon */	{: hasArguments=false; :}
			  	;
MethodDecl ::= StaticOptional RetValue:retVal IDENT:name
			   {:	
			   		if(parser.isStaticFunc && parser.insideOfClass) parser.numStaticFunc++;
			   		if(parser.isStaticFunc && Tab.currentScope == mainScope) 
			   			parser.report_error("Globalna funkcija ne sme biti static", null);
			   		currentMethod = Tab.insert(Obj.Meth, name, retVal);
			   		returnValue = null;
			   		Tab.openScope();
			   		parser.report_info("Obradjuje se funkcija "+name+" na liniji "+nameleft,null);
			   :}
			  LPAREN Parameters RPAREN 
			  {: if("main".equals(name)) parser.stack.push("main"); else parser.stack.push("local"); 
			  	 if("main".equals(name) && hasArguments) parser.report_error("Semanticka greska: Main funkcija mora biti bez argumenata",null);
			  	 if("main".equals(name) && retVal!=Tab.noType) parser.report_error("Semanticka greska: Main funkcija mora biti void",null); 
			  :} 
			  VarDeclListOptional
			  LBRACE 
			  {: 
			   		currentMethod.setAdr(Code.pc);
			   		if ("main".equals(name)) {
			   			Code.mainPc = currentMethod.getAdr();
			   		}
			   		Code.put(Code.enter);
			   		Code.put(currentMethod.getLevel());
			   		Code.put(Tab.currentScope().getnVars());
			   :}
			  StatementList RBRACE
			   {:	
			   		if (!returnFound && retVal != Tab.noType)
			   	 		parser.report_error("Semanticka greska na liniji " + nameleft + ": funcija " + name + " nema return iskaz!", null);
			   		parser.stack.pop();
			   		if(!parser.insideOfClass) parser.numGlobFunc++;
			   		
			   		Code.put(Code.exit);
				 	Code.put(Code.return_);
			   		
					Tab.chainLocalSymbols(currentMethod);
					Tab.closeScope();
					currentMethod = null;
					returnFound = false;			   	
			   :};
// METHOD END --------------------------------------------------------
// CLASSES END -------------------------------------------------------
				
StatementList ::= StatementList Statement
				 |
				 /* */
				 ;

Type ::= IDENT:typeName
{:
	Obj typeNode = Tab.find(typeName);
	if(typeNode ==Tab.noObj){
		parser.report_error("Nije pronadjen tip " + typeName + " u tabeli simbola ", null );
		RESULT = Tab.noType;
	}
	else{
		if(Obj.Type == typeNode.getKind()){
		returnValue = typeNode.getType();
		RESULT = typeNode.getType();
		}
		else {
		parser.report_error("Greska na liniji "+ typeNameleft +  ":Ime " + typeName + " ne predstavlja ime tipa", null);
		RESULT = Tab.noType;
		}
		
	}
	
:}
;


ConditionOptional ::= Condition 
					 |
					 error
					 |
					 /* */
					 ;
					 
DesignatorStatementOptional ::= DesignatorStatement
								|
								error
								|
								/* */
								;

Statement ::= DesignatorStatement SEMI
			  |
			  IF LPAREN Condition RPAREN Statement ELSE Statement
			  |
			  IF LPAREN error RPAREN Statement ELSE Statement
			  |
			  IF LPAREN Condition RPAREN Statement
			  |
			  IF LPAREN error RPAREN Statement
			  |
			  FOR LPAREN DesignatorStatementOptional SEMI ConditionOptional SEMI DesignatorStatementOptional  RPAREN Statement
			  |
			  BREAK SEMI
			  |
			  CONTINUE SEMI
			  |
			  RETURN Expr:t SEMI
			  {:
			  	 returnFound = true;
				 Struct currMethType = currentMethod.getType();
				 if (t!=null && !currMethType.assignableTo(t)) {
				 	parser.report_error("Greska na liniji " + tleft + " : " + "tip izraza u return naredbi ne slaze se sa tipom povratne vrednosti funkcije " + currentMethod.getName(), null);
				 }	
			  :}
			  |
			  RETURN SEMI
			  |
			  READ LPAREN Designator:d RPAREN SEMI
			  {:
			  		if("main".equals(parser.stack.peek())) parser.numFuncCallsInMain++;
			  		
			  		int kind = d.getKind();
				 	if(kind != Obj.Elem && kind != Obj.Var && kind!=Obj.Fld)
				 		parser.report_error("Semanticka greska na liniji "+dleft+ ", pogresan operand za READ ",null);
			  		Struct tip = d.getType();
			  		if(tip.getKind() == Struct.Array) tip = tip.getElemType();
			  		if(tip == parser.boolType || tip == Tab.intType){
			  			Code.put(Code.read);
			  			Code.store(d);
			  		}
			  		else {
				  		if(tip == Tab.charType){
				  			Code.put(Code.bread);
				  			Code.store(d);
				  		}
				  		else
				  			parser.report_error("Semanticka greska na liniji "+dleft+ ", pogresan tip operanda za READ ",null);
			  		}
			  :}
			  |
			  PRINT LPAREN Expr:e RPAREN SEMI
			  {:
			  		if("main".equals(parser.stack.peek())) parser.numFuncCallsInMain++;
			  		if (e != Tab.intType && e != Tab.charType && e!=parser.boolType)
			  	 		parser.report_error("Semanticka greska na liniji " + eleft + ": Operand instrukcije PRINT mora biti char, bool ili int tipa", null);
				  	 
				  	 if (e == Tab.intType || e==parser.boolType) {
				  	 	Code.loadConst(4);
				  	 	Code.put(Code.print);
				  	 }
				  	 
				  	 if (e == Tab.charType) {
				  	 	Code.loadConst(1);
				  	 	Code.put(Code.bprint);
				  	 }
			  		
			  		
			  :}
			  |
			  PRINT LPAREN Expr:e COMMA NUMBER:n RPAREN SEMI
			  {:
			  		if("main".equals(parser.stack.peek())) parser.numFuncCallsInMain++;
			  		if (e != Tab.intType && e != Tab.charType && e!=parser.boolType)
			  	 		parser.report_error("Semanticka greska na liniji " + eleft + ": Operand instrukcije PRINT mora biti char, bool ili int tipa", null);
				  	 
				  	 if (e == Tab.intType || e==parser.boolType) {
				  	 	Code.loadConst(n.intValue());
				  	 	Code.put(Code.print);
				  	 }
				  	 
				  	 if (e == Tab.charType) {
				  	 	Code.loadConst(n.intValue());
				  	 	Code.put(Code.bprint);
				  	 }
			  :}
			  |
			  LBRACE StatementList RBRACE;

ActPars ::= ActParsList
			{: if("main".equals(parser.stack.peek())) parser.numFuncCallsInMain++; :}
			|
			/* */
			{: if("main".equals(parser.stack.peek())) parser.numFuncCallsInMain++; :}
			;
ActParsList ::= ActParsList COMMA Expr
				|
				Expr;


// Dodavanje zbog greske prilikom dodele			  
 DesignatorStatement ::= Designator:d Assignop:assignOp
 						{:	
 						:}
 						 Expr:e
 						{: 
	 						if(d!=Tab.noObj && e!=null){
	 							if(d.getKind()!=Obj.Var && d.getKind()!=Obj.Elem && d.getKind()!=Obj.Fld)
	 								parser.report_error("Greska na liniji "+ assignOpleft +  " sa leve strane operatora = mora biti promenljiva ili element niza", null);
	 							Struct tip = d.getType();
	 								if(tip.getKind() == Struct.Array){
	 								 	if(!e.assignableTo(tip.getElemType()))
	 								 		parser.report_error("Greska na liniji "+ assignOpleft +  " nekompatibilni tipovi ", null);
	 								}
	 								else 
	 									if(!e.assignableTo(tip))
	 								 		parser.report_error("Greska na liniji "+ assignOpleft +  " nekompatibilni tipovi", null);
	 								
	 								
	 							}
	 							if(assignOp == 0) Code.store(d);
	 							else{
		 							Obj node = new Obj(Obj.Var,"",e);
		 							Code.store(node);
		 							if(d.getKind()==Obj.Elem){
		 								Code.put(Code.dup2);
		 							}
		 							Code.load(d);
		 							Code.load(node);
		 							Code.put(assignOp);
		 							Code.store(d);
	 							}
 						:}
 						|
 						Designator Assignop error
 						|
 						 Designator LPAREN ActPars RPAREN
 						|
 						 Designator:d PLUSPLUS:pl
 						 {:	
 						 	RESULT = d;
 						 	int kind = d.getKind();
 						 	if(kind != Obj.Elem && kind != Obj.Var && kind!=Obj.Fld)
 						 		parser.report_error("Semanticka greska na liniji "+plleft+ " nedozvoljen upis",null);
 						 	else{
	 						 	if(d.getType()!=Tab.intType)
	 						 		parser.report_error("Sematicka greska na liniji "+plleft+" Operator '++' se primenjuje samo na tip int",null);
	 						 	else{
	 						 		if(d.getKind()== Obj.Elem) Code.put(Code.dup2);
		 						 	Code.load(d);
		 						 	Code.loadConst(1);
		 						 	Code.put(Code.add);
		 						 	Code.store(d);
		 						 	}	
 						 	}
 						 :}
 						|
 						 Designator:d MINUSMINUS:pl
 						 {:
 						 	RESULT = d;
 						 	int kind = d.getKind();
 						 	if(kind != Obj.Elem && kind != Obj.Var && kind!=Obj.Fld)
 						 		parser.report_error("Semanticka greska na liniji "+plleft+ " nedozvoljen upis",null);
 						 	else{
	 						 	if(d.getType()!=Tab.intType)
	 						 		parser.report_error("Sematicka greska na liniji "+plleft+" Operator '++' se primenjuje samo na tip int",null);
	 						 	else{
	 						 		if(d.getKind()== Obj.Elem) Code.put(Code.dup2);
		 						 	Code.load(d);
		 						 	Code.loadConst(1);
		 						 	Code.put(Code.sub);
		 						 	Code.store(d);
		 						 	}	
 						 	}
 						 :}
 						 ;
 
 RelExprOptional ::= Relop Expr
 					|
 					 /* */
 					;
 


Expr ::= SubExpr:se {: RESULT = se; :};

SubExpr ::= Designator:d AddopRight:ar SubExpr:se
			{:
				if(d!=null && se!=null){
							Struct tip = d.getType();
							if(tip.getKind()==Struct.Array) tip = tip.getElemType();
					   		if(!tip.assignableTo(se)) 
					   			parser.report_error("nekompatibilni tipovi na liniji "+seleft ,null);
					     }
					Obj node = new Obj(Obj.Var, "", Tab.intType);
					Code.store(node);
					if(d!=null){
						if(d.getKind() ==Obj.Elem){
							Code.put(Code.dup2);
							Code.put(Code.dup2);
						}
						Code.load(d);
						Code.load(node);
						Code.put(ar);
						Code.store(d);
						Code.load(d);
					}
					
				RESULT = se;
			:}
			|
			SubExprLeft:sel {: RESULT = sel;:}
			;
			
SubExprLeft ::= SubExprLeft:sel AddopLeft:al Term:t
				{:
					if(t!=null && sel!=null)
						if(!t.assignableTo(sel)) parser.report_error("SEL nekompatibilni tipovi na liniji "+selleft+ " "+sel.getKind(),null);
					Code.put(al);
					RESULT = t;
				:}
				|
				Term:t {: RESULT = t; :}
				|
				MINUS Term:t 
				{:	
					Obj node = new Obj(Obj.Con, "", t);
					node.setAdr(-1);
					Code.load(node);
					Code.put(Code.mul);
				 	RESULT = t; 
				:}
				;
				

Term ::= FactorExpr:fe {: RESULT = fe; :};




FactorExpr ::= Designator:d MulopRight:mr FactorExpr:fel
				{:	
					if(d!=null && fel!=null){
							Struct tip = d.getType();
							if(tip.getKind()==Struct.Array) tip = tip.getElemType();
					   		if(!tip.assignableTo(fel)) 
					   			parser.report_error("nekompatibilni tipovi na liniji "+felleft ,null);
					     }
					Obj node = new Obj(Obj.Var, "", Tab.intType);
					Code.store(node);
					if(d!=null){
						if(d.getKind() ==Obj.Elem){
							Code.put(Code.dup2);
							Code.put(Code.dup2);
						}
						Code.load(d);
						Code.load(node);
						Code.put(mr);
						Code.store(d);
						Code.load(d);
					}
					
					RESULT = fel;
				  :}
				|
				FactorExprLeft:fel
				{:
					RESULT = fel;
				:}
				;

				
FactorExprLeft ::= FactorExprLeft:fel MulopLeft:ml Factor:f
				   {: 
					   if(f!=null && fel!=null){
					   		if(!f.assignableTo(fel)) parser.report_error("nekompatibilni tipovi na liniji "+felleft,null);
					     }
					     Code.put(ml); 
					     RESULT = f;
				    :}
					|
				   Factor:f
				   {: if(f!=null) RESULT = f; :}
				   ;


Factor ::= Designator:d 
		 	{: 			
					  	RESULT = d.getType();
					  	Code.load(d);
			:}
			| 
		   Designator LPAREN ActPars RPAREN
			|
		   NUMBER:i 
		   {:	
		   		Obj c = Tab.insert(Obj.Con, "", Tab.intType);
				c.setAdr(i.intValue());
				Code.load(c);
				RESULT = Tab.intType; 
		   :}
		   |
		   CHAR:ch
		   {:	
		   		Obj c = Tab.insert(Obj.Con, "", Tab.charType);
				c.setAdr(ch.charValue());
				Code.load(c);
				RESULT = Tab.charType; 
		   :}
		   |
		   BOOL:b
		   {:	
		   		Obj c = Tab.insert(Obj.Con, "", parser.boolType);
				c.setAdr(b.intValue());
				Code.load(c);
				RESULT = parser.boolType; 
		   :}
		   |
		   NEW Type:t LZAG Expr:e RZAG
		   {:
		   		if(t!=Tab.intType && t!=Tab.charType && t!=parser.boolType)
		   			parser.report_error("Greska: nizovi mogu biti samo osnovnih tipova, char, int ili bool",null);
		   		else{
			   		if(e!=Tab.intType)
			   			parser.report_error("Greska: broj elemenata niza mora biti tipa int",null);
			   		else{
			   			Code.put(Code.newarray);
			   			if(t==Tab.charType) Code.put(0);
			   			else Code.put(1);
			   		}
		   		}
		   			
		   :}
		   |
		   NEW Type
		   |
		   LPAREN Expr:e RPAREN {: RESULT = e; :}
		   ;

DesignatorListElement ::= POINT IDENT {: array = null; :}
						   |
						  LZAG:lzag
						  {:	
						  		if(array!=null){
						  			RESULT = Tab.noObj;
							  		if(array.getType().getKind()!=Struct.Array)
							  			parser.report_error("Sematicka greska na liniji "+lzagleft +", identifikator nije niz",null);
							  		else{
							  			Code.load(array);
							  			RESULT = new Obj(Obj.Elem, "", array.getType().getElemType());	
							  		}
							  		array = null;
						  		}
						  :}
						   Expr:e RZAG:rzag
						  {:	
						  		if(e!=Tab.intType)
						  			parser.report_error("Sematicka greska na liniji "+rzagleft +", Indeks niza mora biti tipa int",null);
						  :}
						  ;

DesignatorList ::=  DesignatorList DesignatorListElement:dle {: RESULT = dle; :}
					|
					DesignatorListElement:dle {:  RESULT = dle;	:}
					;
					

Designator ::= IDENT:varName {: array =  parser.checkExistence(varNameleft, varName); :}
			   DesignatorList:dl
			    {: 
			   		RESULT = dl;
		
			    :}
				|
			   IDENT:varName {:
			   		numLine = varNameleft;
			   		varIdent = varName;
			   		
			   		RESULT = parser.checkExistence(numLine, varIdent);
			   	:};



 
 CondFact ::= Expr RelExprOptional;
 
 
 CondFactList ::= CondFact
 				  |
 				  CondFact AND CondFactList;
 
 CondTerm ::= CondFactList;
 
 CondTermList::= CondTerm
 				 |
 				 CondTerm OR CondTermList;
 
 Condition ::= CondTermList;
 
 
 Assignop ::= EQUAL			  {: RESULT = 0;	:}
 			  |
 			  AddopRight:ar	  {: RESULT = ar;	:}
 			  |
 			  MulopRight:mr   {: RESULT = mr;	:}
 			  ;
 
 AddopRight ::= PLUSEQUAL	  {: RESULT = Code.add; :}
 				|
 				MINUSEQUAL	  {: RESULT = Code.sub; :}
 				;
 				
 MulopRight ::= MULTIPLYEQUAL {: RESULT = Code.mul; :}
 				|
 				DIVIDEEQUAL   {: RESULT = Code.div; :}
 				|
 				ME			  {: RESULT = Code.rem; :}
 				;
 				
// Addop ::= AddopRight
// 			|
// 		   AddopLeft;
 
 AddopLeft ::= PLUS		{: RESULT = Code.add; :}
 				|
 			   MINUS	{: RESULT = Code.sub; :} 
 			   ;
 			   
 //Mulop ::= MulopLeft
 //		   |
 //		   MulopRight;
 		   
 MulopLeft ::= MULTIPLY {: RESULT = Code.mul; :}
 			   |
 			   DIVIDE   {: RESULT = Code.div; :}
 			   |
 			   MOD		{: RESULT = Code.rem; :}
 			   ;
 			   
 Relop ::= EE
 		   |
 		   NOTEQUAL
 		   |
 		   LESS
 		   |
 		   LESSEQUAL
 		   |
 		   GREATER
 		   |
 		   GREATEREQUAL;